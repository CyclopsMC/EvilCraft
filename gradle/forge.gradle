repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'Player Maven FS'
        url 'http://maven.ic2.player.to/'
    }
    maven {
        name "Cyclops Repo"
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
    maven {
        name "Tehnut Repo"
        url "http://tehnut.info/maven/"
    }
}
dependencies {
    // Add something like 'cyclopscore_version_local=0.1.0-DEV' to your gradle.properties if you want to use a custom local CyclopsCore version.
    if(project.hasProperty("cyclopscore_version_local")) {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${project.cyclopscore_version_local}:deobf"
    } else {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${config.cyclopscore_version}:deobf"
    }

    deobfCompile "slimeknights.mantle:Mantle:${config.minecraft_version}-${config.mantle_version}" // https://dvs1.progwml6.com/jenkins/
    deobfCompile "slimeknights:TConstruct:${config.minecraft_version}-${config.tconstruct_version}" // http://dvs1.progwml6.com/files/maven/slimeknights/TConstruct/maven-metadata.xml
    compile "mcp.mobius.waila:Waila:${config.waila_version}:dev" // http://mobiusstrip.eu/maven/mcp/mobius/waila/Waila/
    //compile "net.sengir.forestry:forestry_${config.minecraft_version}:${config.forestry_version}:dev" // http://maven.ic2.player.to/
    //compile "com.pahimar.ee3:EquivalentExchange3:${config.minecraft_version}-${config.EE3_version}:dev" // http://files.minecraftforge.net/maven/com/pahimar/ee3/EquivalentExchange3
    //compile "net.industrial-craft:industrialcraft-2:${config.IC2_version}:dev" // http://maven.ic2.player.to/
    deobfCompile "com.wayoftime.bloodmagic:BloodMagic:${config.minecraft_version}-${config.BM_version}" // http://tehnut.info/maven/com/wayoftime/bloodmagic/BloodMagic
    deobfCompile "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}" // https://dvs1.progwml6.com/jenkins/

    // Project lombok
    compile "org.projectlombok:lombok:1.14.4"
}


minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"
    runDir = "eclipse/assets"
    useDepAts = true
    
    replaceIn "Reference.java"
    replace "@VERSION@", project.version
    replace "@MC_VERSION@", project.config.minecraft_version
    replace "@FORGE_VERSION@", project.config.forge_version
    replace "@BUILD_NUMBER@", project.buildnumber
    replace "@CYCLOPSCORE_VERSION@", project.config.cyclopscore_version

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "${config.mcp_mappings_version}"
}

if (project.buildnumber.equals("RELEASE"))
    version = "${config.minecraft_version}-${config.mod_version}"
else
    version = "${config.minecraft_version}-${config.mod_version}-${buildnumber}"

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, that is not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
