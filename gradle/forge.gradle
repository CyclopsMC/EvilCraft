repositories {
    mavenCentral()
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'Player Maven FS'
        url 'http://maven.ic2.player.to/'
    }
    ivy {
        name "Cyclops Core"
        artifactPattern "http://addons-origin.cursecdn.com/files/2247/323/[module]-[revision].[ext]"
    }
}

dependencies {
    // Add something like 'cyclopscore_version_local=0.1.0-DEV' to your gradle.properties if you want to use a custom local CyclopsCore version.
    if(project.hasProperty("cyclopscore_version_local")) {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${project.cyclopscore_version_local}:deobf"
    } else {
        compile group: 'org.cyclops', name: 'CyclopsCore', version: "${config.minecraft_version}-${config.cyclopscore_version}-deobf", ext: 'jar'
    }

    //compile "tconstruct:TConstruct:${config.minecraft_version}-${config.tconstruct_version}:deobf" // http://dvs1.progwml6.com/files/maven
    compile "mcp.mobius.waila:Waila:${config.waila_version}:dev" // http://mobiusstrip.eu/maven/mcp/mobius/waila/Waila/
    //compile "codechicken:ForgeMultipart:${config.minecraft_version}-${config.FMP_version}:dev" // http://files.minecraftforge.net/ForgeMultipart/
    //compile "net.sengir.forestry:forestry_${config.minecraft_version}:${config.forestry_version}:dev" // http://maven.ic2.player.to/
    compile "codechicken:NotEnoughItems:${config.minecraft_version}-${config.NEI_version}:dev" // http://chickenbones.net/maven/codechicken/NotEnoughItems/
    compile "codechicken:CodeChickenCore:${config.minecraft_version}-${config.CCC_version}:dev" // http://chickenbones.net/maven/codechicken/CodeChickenCore/
    compile "codechicken:CodeChickenLib:${config.minecraft_version}-${config.CCL_version}:dev" // http://files.minecraftforge.net/CodeChickenLib/
    //compile files("${project.libs_path}/BloodMagic-${config.minecraft_version}-${config.BM_version}-deobf.jar") // https://www.dropbox.com/sh/0aqvalqobu61t44/AADZq-GuoDeuNzgg6zueVM_Ca

    // Project lombok
    compile "org.projectlombok:lombok:1.14.4"
}

minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"
    runDir = "eclipse/assets"
    
    replaceIn "Reference.java"
    replace "@VERSION@", project.version
    replace "@MC_VERSION@", project.config.minecraft_version
    replace "@FORGE_VERSION@", project.config.forge_version
    replace "@BUILD_NUMBER@", project.buildnumber
    replace "@CYCLOPSCORE_VERSION@", project.config.cyclopscore_version

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20150401"
}

if (System.getenv().RELEASE)
    version = "${config.minecraft_version}-${config.mod_version}"
else
    version = "${config.minecraft_version}-${config.mod_version}-${buildnumber}"

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, that is not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
